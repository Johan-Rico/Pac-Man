class Ghosts {
    field Map map;
    field PacMan pacman;
    field Array ghosts;
    field int nGhosts;
    field boolean color;
    field boolean ghostsVulnerable;
    field int vulnerableTimer;
	field Score score;
    
    constructor Ghosts new(boolean c, Map m, PacMan p) {
        let map = m;
        let pacman = p;
        let color = c;
        let nGhosts = 5;
        let ghostsVulnerable = false;
        let vulnerableTimer = 0;
        do createGhosts(color);
        return this;
    }
    
    method void init() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.setVulnerable(false);
            do g.changeColor(color);
            do g.init();
            let i = i + 1;
        }
        do draw();
        return;
    }

	method void setScore(Score s) {
        let score = s;
        return;
    }
    
    method void createGhosts(boolean color) {
        let ghosts = Array.new(nGhosts);
        
        let ghosts[0] = Ghost.new(color, map, pacman, 256, 108, true, 0);
        let ghosts[1] = Ghost.new(color, map, pacman, 241, 135, true, 100); 
        let ghosts[2] = Ghost.new(color, map, pacman, 268, 135, true, 200);  
        let ghosts[4] = Ghost.new(color, map, pacman, 241, 153, false, 300); 
        let ghosts[3] = Ghost.new(color, map, pacman, 268, 153, false, 400); 
        
        return;
    }
    
    method void next() {
        var Ghost g;
        var int i;
        
        if (ghostsVulnerable) {
            let vulnerableTimer = vulnerableTimer - 1;
            if (vulnerableTimer < 0) {
                let ghostsVulnerable = false;
                do changeGhostsColor(color);
                let i = 0;
                while (i < nGhosts) {
                    let g = ghosts[i];
                    do g.setVulnerable(false);
                    let i = i + 1;
                }
            }
        }
        

        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.next();
            let i = i + 1;
        }
        return;
    }
    
    method void makeVulnerable() {
        var Ghost g;
        var int i;
        let ghostsVulnerable = true;
        let vulnerableTimer = 300;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.setVulnerable(true);
            let i = i + 1;
        }
        do changeGhostsColor(~color);
        return;
    }
    
    method void changeColor(boolean c) {
        let color = c;
        do changeGhostsColor(color);
        return;
    }

    method void changeGhostsColor(boolean c) {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.changeColor(c);
            let i = i + 1;
        }
        return;
    }
    
    method void draw() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.draw();
            let i = i + 1;
        }
        return;
    }
    
    method void clearGhosts() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.changeColor(color);
            do g.clearGhost();
            let i = i + 1;
        }
        return;
    }
    
    method boolean checkPacmanCollision() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            if (g.checkPacmanCollision()) {
                if (g.isVulnerable()) {
                    do g.changeColor(color);
                    do g.clearGhost();
                    do g.reset();
                    do score.addPoints(200);
                    do g.setVulnerable(false);
                } else {
                    return true;
                }
            }
            let i = i + 1;
        }
        return false;
    }
    
    method void dispose() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.dispose();
            let i = i + 1;
        }
        do Memory.deAlloc(this);
        return;
    }
}