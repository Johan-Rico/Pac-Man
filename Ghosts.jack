class Ghosts {
    field Map map;
    field PacMan pacman;
    field Array ghosts;
    field Score score;
    field int nGhosts;
    field boolean ghostsVulnerable;
    field int vulnerableTimer;
    
    constructor Ghosts new(boolean c, Map m, PacMan p, Score s) {
        let map = m;
        let pacman = p;
        let score = s;
        let nGhosts = 5;
        let ghostsVulnerable = false;
        let vulnerableTimer = 0;
        do createGhosts(c);
        return this;
    }
    
    method void init() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.setVulnerable(false);
            do g.init();
            let i = i + 1;
        }
        do draw();
        return;
    }
    
    method void createGhosts(boolean color) {
        let ghosts = Array.new(nGhosts);
        
        let ghosts[0] = Ghost.new(color, map, pacman, 256, 108, false, 0);
        let ghosts[1] = Ghost.new(color, map, pacman, 241, 135, true, 150); 
        let ghosts[2] = Ghost.new(color, map, pacman, 268, 135, false, 300);  
        let ghosts[4] = Ghost.new(color, map, pacman, 241, 153, true, 450); 
        let ghosts[3] = Ghost.new(color, map, pacman, 268, 153, false, 600); 
        
        return;
    }
    
    method void next() {
        var Ghost g;
        var int i, t, f;
        let t = 75;
        let f = t / 9;
        
        if (ghostsVulnerable) {
            let vulnerableTimer = vulnerableTimer - 1;
            if (vulnerableTimer = t) { do setBlinking(true); }
            if (vulnerableTimer = (t - f)) { do setBlinking(false); }
            if (vulnerableTimer = (t - (2*f))) { do setBlinking(true); }
            if (vulnerableTimer = (t - (3*f))) { do setBlinking(false); }
            if (vulnerableTimer = (t - (4*f))) { do setBlinking(true); }
            if (vulnerableTimer = (t - (5*f))) { do setBlinking(false); }
            if (vulnerableTimer = (t - (6*f))) { do setBlinking(true); }
            if (vulnerableTimer = (t - (7*f))) { do setBlinking(false); }
            if (vulnerableTimer = (t - (8*f))) { do setBlinking(true); }
            if (vulnerableTimer < 0) {
                let ghostsVulnerable = false;
                let i = 0;
                while (i < nGhosts) {
                    let g = ghosts[i];
                    do g.setBlinking(false);
                    do g.setVulnerable(false);
                    let i = i + 1;
                }
            }
        } 

        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.drawClosePellets(score);
            do g.next();
            let i = i + 1;
        }
        return;
    }

    method void setBlinking(boolean b) {
        var Ghost g;
        var int i;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.setBlinking(b);
            let i = i + 1;
        }
        return;
    }
    
    method void makeVulnerable() {
        var Ghost g;
        var int i;
        let ghostsVulnerable = true;
        let vulnerableTimer = 300 - ((Game.getLevel()-1) * 50);
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.setBlinking(false);
            do g.setVulnerable(true);
            let i = i + 1;
        }
        return;
    }
    
    method void changeColor(boolean c) {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.changeColor(c);
            let i = i + 1;
        }
        return;
    }
    
    method void draw() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.draw();
            let i = i + 1;
        }
        return;
    }
    
    method void clearGhosts() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.clear();
            let i = i + 1;
        }
        return;
    }
    
    method boolean checkPacmanCollision() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            if (g.checkPacmanCollision()) {
                if (g.isVulnerable()) {
                    do Sys.wait(700);
                    do score.addPoints(200);
                    do g.clear();
                    do g.setDead();
                    do g.setVulnerable(false);
                } else {
                    if (~g.isDead()) {
                        return true;
                    }
                }
            }
            let i = i + 1;
        }
        return false;
    }
    
    method void dispose() {
        var Ghost g;
        var int i;
        let i = 0;
        while (i < nGhosts) {
            let g = ghosts[i];
            do g.dispose();
            let i = i + 1;
        }
        do Memory.deAlloc(this);
        return;
    }
}