class Wall {
  // corners of wall
  field int leftX, topY, rightX, bottomY;

  // construct wall
  constructor Wall new(int x1, int y1, int x2, int y2) {
    let leftX = x1;
    let topY = y1;
    let rightX = x2;
    let bottomY = y2;
    return this;
  }

  // draw wall rectangle
  method void draw() {
    do Screen.setColor(false);
    do Screen.drawRectangle(leftX, topY, rightX, bottomY);
    return;
  }

  // check if object is overlapping wall given its coordinates
  method boolean checkOverlapping(int x, int y, int radius) {
    return (
      // left of object overlaps right of wall
      // right of object overlaps left of wall
      ((x - radius) < (rightX + 2)) & ((x+radius) > (leftX - 2))
    ) & (
      // top of object overlaps bottom of wall
      // bottom of object overlaps top of wall
      ((y-radius) < (bottomY + 2)) & ((y+radius) > (topY - 2))
    );
  }

  // dispose wall
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }
}