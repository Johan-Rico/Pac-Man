class Game {

	field PacMan pacman;
	field int score;
	field Map map;
	field boolean color;
	
	constructor Game new(boolean c) {
		let color = c;
		do Output.setColor(~color);
		do clearScreen();
		do createSprites();
		return this;
	}

	method void init() {
		do map.init();
		do pacman.init();
		return;
	}

	method void next() {
		do pacman.next();
		return;
	}
	
	method void handleKey(int key) {
		do pacman.handleKey(key);
		return;
	}

	method void clearScreen() {
		var int x, y;
        do Screen.setColor(color);
		do Screen.drawRectangle(0, 0, 511, 255);
		do Screen.setColor(~color);
		return;
	}

	method void newGame() {
		do disposeSprites();
		do clearScreen();
		do createSprites();
		do init();
		return;
  	}

	method void createSprites() {
		var int x;
		var int y;
		let y = 29;
		let map = Map.new(~color);
		let pacman = PacMan.new(~color, map);
		while (y < 255) {
			let x = 8;
			while (x < 511) {
				do Screen.drawRectangle(x, y, x+1, y+1);
				let x = x + 9;
			}
			let y = y + 9;
		}
		return;
	}
		
  	method void disposeSprites() {
		do pacman.dispose();
		do map.dispose();
		return;
	}

  	method void dispose() {
		do Memory.deAlloc(this);
		return;
  	}
}