class Game {
	field PacMan pacman;
	field Ghosts ghosts;
	field Map map;
	field Score score;
	field boolean color;
	field int lives;
	static int level;
	
	constructor Game new(boolean c) {
		let color = c;
		let lives = 3;
		let level = 1;
		let map = Map.new(~color);
		let score = Score.new(~color, map);
		let pacman = PacMan.new(~color, map, score);
		let ghosts = Ghosts.new(~color, map, pacman, score);
		return this;
	}

	method void init() {
		do Output.setColor(~color);
		do clearScreen();
		do printLives();
		do map.init();	
		do score.init();
		do startLevel();
		return;
	}

	function int getLevel () {
		return level;
	}

	method boolean next() {
		do pacman.next();
		do ghosts.next();
		if (score.checkPowerPelletEaten()) {
			do ghosts.makeVulnerable();
		}
		do map.drawDoor();
		if (score.allPelletsCollected()) {
			do newLevel();
		} else {
		if (ghosts.checkPacmanCollision()) {
			return handlePacmanDeath();
		}}
		return true;
	}
	
	method void handleKey(int key) {
		if (key = 144) {
			do changeColor(true);
		}
		if (key = 145) {
			do changeColor(false);
		}
		do pacman.handleKey(key);
		return;
	}

	method void clearScreen() {
		var int x, y;
        do Screen.setColor(color);
		do Screen.drawRectangle(0, 0, 511, 255);
		do Screen.setColor(~color);
		return;
	}

	method void newGame() {
		do clearScreen();
		do init();
		return;
  	}

	method void changeColor(boolean c) {
		let color = c;
		do clearScreen();
		do printLives();
		do ghosts.changeColor(~color);
		do map.changeColor(~color);
		do pacman.changeColor(~color);
		do score.changeColor(~color);
		return;
	}

	method void printLives() {
		var int i;
		let i = 0;
		while (i < (lives - 1)) {	
			do printLive(470-(20*i), 14);
			let i = i + 1;
		}
		return;
	}

	method void printLive(int x, int y) {
		var int i;
		do Screen.setColor(~color);
		do Screen.drawCircle(x, y, 7);
		let i = -2;
		do Screen.setColor(color);
		while (~(i > 2)) {
			do Screen.drawLine(x+(3*Math.abs(i))-1, y+i, x+7, y+i);
			let i = i + 1;
		}
		return;
	}

	method void newLevel() {
		let level = level + 1;
		do Sys.wait(2000);
		do ghosts.clearGhosts();
		do map.drawDoor();
		do clearScreen();
		do Sys.wait(100);
		do map.draw();
		do printLives();
		do score.nextLevel();
		do startLevel();
		return;
	}

	method void startLevel() {
		do pacman.init();
		do ghosts.init();
		do Output.moveCursor(16,28);
		do Output.printString(" READY!");
		do Sys.wait(2500);
		do Output.moveCursor(16,28);
		do Output.printString("       ");
		return;
	}

	method boolean handlePacmanDeath() {
		let lives = lives - 1;
		
		do Sys.wait(750);
		do ghosts.clearGhosts();
		do score.drawPellets();
		do map.drawDoor();
		do pacman.deathAnimation();
		do Sys.wait(2000);
		if (lives > 0) {
			do Screen.setColor(color);
			do Screen.drawRectangle(463-(20*(lives)), 7, 477-(20*(lives))+20, 21);
			do startLevel();
			return true;
		}
		do Output.moveCursor(16,26);
		do Output.printString(" GAME  OVER");
		do Sys.wait(3000);
		do dispose();
		return false;
	}

	method void dispose() {
			do pacman.dispose();
			do ghosts.dispose();
			do map.dispose();
			do score.dispose();
			do Memory.deAlloc(this);
			return;
		}
	}